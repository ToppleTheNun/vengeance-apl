# Soul fragment thresholds for Spirit Bomb usage
$(st_spb_thresh_fd)=5
$(small_aoe_spb_thresh_fd)=5
$(big_aoe_spb_thresh_fd)=4

$(st_spb_thresh)=5
$(small_aoe_spb_thresh)=5
$(big_aoe_spb_thresh)=4

# Felblade condition, used by both Felblade and Vengeful Retreat
$(should_felblade_ar)=(((talent.spirit_bomb&(fury<40&(variable.can_spb|variable.can_spb_soon)))|(talent.spirit_bomb&(cooldown.sigil_of_spite.up|cooldown.soul_carver.up)&cooldown.fel_devastation.up&fury<50))|fury<30)

# Spirit Bomb conditions
actions.ar+=/variable,name=spb_threshold,op=setif,condition=$(fd_active),value=(variable.single_target*$(st_spb_thresh_fd))+(variable.small_aoe*$(small_aoe_spb_thresh_fd))+(variable.big_aoe*$(big_aoe_spb_thresh_fd)),value_else=(variable.single_target*$(st_spb_thresh))+(variable.small_aoe*$(small_aoe_spb_thresh))+(variable.big_aoe*$(big_aoe_spb_thresh))
actions.ar+=/variable,name=can_spb,value=soul_fragments>=variable.spb_threshold
actions.ar+=/variable,name=can_spb_soon,value=soul_fragments.total>=variable.spb_threshold
actions.ar+=/variable,name=can_spb_one_gcd,value=(soul_fragments.total+variable.num_spawnable_souls)>=variable.spb_threshold
# Don't soul cleave if we are able to use spirit bomb soon or are building to spirit bomb threshold
actions.ar+=/variable,name=dont_soul_cleave,value=variable.can_spb|variable.can_spb_soon|variable.can_spb_one_gcd|prev_gcd.1.fracture
# During Reaver's Glaive, enhance our soul cleave (1) or shear (0)
actions.ar+=/variable,name=rg_enhance_cleave,op=setif,condition=variable.big_aoe|fight_remains<10,value=1,value_else=0
# Externals, potions, trinkets when reaver's mark is up and we have thrill of the fight damage up
actions.ar+=/variable,name=cooldown_sync,value=(debuff.reavers_mark.up&buff.thrill_of_the_fight_damage.up)|fight_remains<20

actions.ar+=/potion,use_off_gcd=1,if=variable.cooldown_sync
actions.ar+=/use_items,use_off_gcd=1,if=variable.cooldown_sync
actions.ar+=/call_action_list,name=externals,if=variable.cooldown_sync
actions.ar+=/run_action_list,name=rg_active,if=buff.glaive_flurry.up|buff.rending_strike.up
actions.ar+=/call_action_list,name=ar_execute,if=fight_remains<20

# Time metamorphosis to optimize soul fragment generation. Don't use it when we know we're not going to be pressing fracture soon.
actions.ar+=/metamorphosis,use_off_gcd=1,if=!buff.metamorphosis.up&!(cooldown.the_hunt.up|buff.reavers_glaive.up)
# Use Vengeful Retreat to reset felblade if we take unhindered assault and want to felblade
actions.ar+=/vengeful_retreat,use_off_gcd=1,if=talent.unhindered_assault&!cooldown.felblade.up&$(should_felblade_ar)
# Always maintain immolation aura for fury generation
actions.ar+=/immolation_aura
# Always maintain sigil of flame for fury generation. If we have illuminated sigils and ascending flame, it's okay to double stack. If we don't, make sure the dot is about to fall off before we reapply (can time this with sigil activation time).
actions.ar+=/sigil_of_flame,if=talent.ascending_flame|(!talent.ascending_flame&!prev_gcd.1.sigil_of_flame&(dot.sigil_of_flame.remains<(1+talent.quickened_sigils)))
# If Reaver's Mark is going to fall off soon and consuming our currently available soul fragments would generate a new Reaver's Glaive in one GCD, we want to do so immediately. Do this with Spirit Bomb if we have enough souls, Soul Cleave if we only need 2 souls to be consumed, or Bulk Extraction if we need mroe than 2 souls and aren't at a Spirit Bomb threshold.
actions.ar+=/soul_cleave,if=(debuff.reavers_mark.remains<=($(this_action)+(gcd.max*2)))&(buff.art_of_the_glaive.stack+soul_fragments>=30&buff.art_of_the_glaive.stack>=28)&(fury<40|!variable.can_spb)
actions.ar+=/spirit_bomb,if=(debuff.reavers_mark.remains<=($(this_action)+(gcd.max*2)))&(buff.art_of_the_glaive.stack+soul_fragments>=30)
actions.ar+=/bulk_extraction,if=(debuff.reavers_mark.remains<=($(this_action)+(gcd.max*2)))&(buff.art_of_the_glaive.stack+(spell_targets>?5)>=30)


# Time reaver's glaive such that our empowered abilities are cast while the previous reaver's glaive is still active. We usually have about 4 GCDs worth of actions to take: Reaver's Glaive -> Fiery Brand / The Hunt  -> Soul Cleave -> Fracture
# If The Hunt will be coming off cooldown soon, we can Reaver's Glaive early to ensure The Hunt can be cast ASAP.
# If we are enhancing cleave, we just want to send this ASAP. If we are enhancing fracture, we want to delay until the end of previous reavers mark to ensure full reavers mark uptime.
actions.ar+=/reavers_glaive,if=(buff.art_of_the_glaive.stack+soul_fragments>=30)|(debuff.reavers_mark.remains<=($(this_action)+(gcd.max*4)))|cooldown.the_hunt.remains<($(this_action)+(gcd.max*4))|variable.rg_enhance_cleave
# Don't use The Hunt if we have a Reaver's Glaive up, they don't stack
actions.ar+=/the_hunt,if=!buff.reavers_glaive.up
# Fiery Brand if we're going to overcap or it's not currently active.
actions.ar+=/fiery_brand,if=!talent.fiery_demise|(talent.fiery_demise&((talent.down_in_flames&charges>=max_charges)|(active_dot.fiery_brand=0)))
# Time Fel Devastation with high priority when we can channel while waiting for soul fragments to spawn
actions.ar+=/fel_devastation,if=talent.spirit_bomb&!variable.can_spb&(variable.can_spb_soon|soul_fragments.inactive>=2)
# If we can use Spirit Bomb, do so
actions.ar+=/spirit_bomb,if=variable.can_spb
# Fracture if we need fury to use Spirit Bomb but felblade and vengeful retreat are not ready, or if we need both fury and soul fragments to use Spirit Bomb
actions.ar+=/fracture,if=talent.spirit_bomb&((fury<40&(!cooldown.felblade.up&(!talent.unhindered_assault|!cooldown.vengeful_retreat.up)))|(fury<40&variable.can_spb_one_gcd))
# Soul Carver when we won't overcap on soul fragments and will be able to Spirit Bomb soon to use them. Don't use it if we've just cast Sigil of Spite, since we'll overcap on soul fragments.
actions.ar+=/soul_carver,if=!talent.spirit_bomb|(((soul_fragments.total+3)<=6)&fury>=15&!prev_gcd.1.sigil_of_spite)
# Sigil of Spite right before a spirit bomb (at 4-5 soul fragments) so that the new soul fragments arrive just after we spend the current ones with a spirit bomb. We can also use this if we happen to be at a single soul fragment and don't have any incoming.
actions.ar+=/sigil_of_spite,if=!talent.spirit_bomb|((variable.can_spb&fury>=40)|variable.can_spb_soon|soul_fragments<=1)
# Fel Devastation only during thrill of the fight in single target
actions.ar+=/fel_devastation,if=!variable.single_target|buff.thrill_of_the_fight_damage.up
# Bulk extraction is only worth casting at 5+ targets
actions.ar+=/bulk_extraction,if=spell_targets>=5
# Felblade to enable a Spirit Bomb or Fel Devastation
actions.ar+=/felblade,if=$(should_felblade_ar)
# Soul Cleave when we're about to overcap on fury, are using a non-Spirit Bomb build, or we're not using Spirit Bomb soon.
actions.ar+=/soul_cleave,if=fury.deficit<=25|(!talent.spirit_bomb|!variable.dont_soul_cleave)
actions.ar+=/fracture
actions.ar+=/shear
actions.ar+=/felblade
actions.ar+=/throw_glaive


# Meta can be used before the fracture if we won't waste souls (it will generate 4 souls)
actions.rg_active+=/metamorphosis,use_off_gcd=1,if=!buff.metamorphosis.up&(buff.rending_strike.up&!buff.glaive_flurry.up)&soul_fragments<=1
# Felblade if we need fury to use Reaver's Glaive buffs
actions.rg_active+=/felblade,if=fury<30&!variable.rg_enhance_cleave&buff.rending_strike.up&buff.glaive_flurry.up
# Use The Hunt before the rest of rg_active if doing so does not cause reavers mark to fall off -- this is to get more uptime on the hunt
actions.rg_active+=/the_hunt,if=!buff.reavers_glaive.up&(debuff.reavers_mark.remains>($(this_action)+action.soul_cleave.execute_time+(talent.fracture&action.fracture.execute_time|!talent.fracture&action.shear.execute_time)+gcd.max))
# Use RG buffs in the order that the variable rg_enhance_cleave dictates
actions.rg_active+=/fracture,if=variable.rg_enhance_cleave&buff.rending_strike.up&buff.glaive_flurry.up|!variable.rg_enhance_cleave&!buff.glaive_flurry.up
actions.rg_active+=/shear,if=variable.rg_enhance_cleave&buff.rending_strike.up&buff.glaive_flurry.up|!variable.rg_enhance_cleave&!buff.glaive_flurry.up
# Bulk Extraction to get a Soul Furnace damage amp up if we're not already at a damage amp
actions.rg_active+=/bulk_extraction,if=!buff.soul_furnace_damage_amp.up&buff.soul_furnace_stack.stack+(spell_targets>?5)>=10
actions.rg_active+=/soul_cleave,if=!variable.rg_enhance_cleave&buff.glaive_flurry.up&buff.rending_strike.up|variable.rg_enhance_cleave&!buff.rending_strike.up
# Filler just in case.
actions.rg_active+=/felblade
# We can't use Fracture as filler until its buff has been consumed.
actions.rg_active+=/fracture,if=!buff.rending_strike.up

# During the last few seconds of a fight, send whatever cooldowns we have left.
actions.ar_execute=metamorphosis,use_off_gcd=1
actions.ar_execute+=/reavers_glaive
actions.ar_execute+=/the_hunt,if=!buff.reavers_glaive.up
actions.ar_execute+=/bulk_extraction,if=spell_targets>=3&buff.art_of_the_glaive.stack>=20
actions.ar_execute+=/sigil_of_flame
actions.ar_execute+=/fiery_brand
actions.ar_execute+=/sigil_of_spite
actions.ar_execute+=/soul_carver
actions.ar_execute+=/fel_devastation